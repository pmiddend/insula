FIND_PACKAGE(
	Boost 1.40.0 REQUIRED COMPONENTS
	unit_test_framework
)

FUNCTION(
	ADD_INSULA_TEST
	PATHNAME
)
	STRING(REGEX REPLACE "\\/" "_" TESTNAME ${PATHNAME})

	SET(INSULA_TEST_NAME insula_${TESTNAME}_test)

	ADD_EXECUTABLE(${INSULA_TEST_NAME} ${PATHNAME}.cpp ${ARGN})

	IF(MSVC)
		SET_PROPERTY(
			TARGET ${INSULA_TEST_NAME}
			PROPERTY COMPILE_FLAGS
			"-DBOOST_TEST_NO_LIB -DBOOST_TEST_MODULE=${TESTNAME}"
		)
	ELSE()
		SET_PROPERTY(
			TARGET ${INSULA_TEST_NAME}
			PROPERTY COMPILE_FLAGS
			"-DBOOST_TEST_DYN_LINK -DBOOST_TEST_MODULE=${TESTNAME}"
		)
	ENDIF()

	TARGET_LINK_LIBRARIES (
		${INSULA_TEST_NAME}
		${INSULA_DEFAULT_LINK_TARGET}
		${Boost_UNIT_TEST_FRAMEWORK_LIBRARY}
		fcppt
		sgecore
	)

	ADD_TEST(
		NAME
		${INSULA_TEST_NAME}
		COMMAND
		${INSULA_TEST_NAME}
	)
ENDFUNCTION()

ADD_INSULA_TEST(display_2d_grid)
ADD_INSULA_TEST(calculate_index)
ADD_INSULA_TEST(convolute)
ADD_INSULA_TEST(frustum)
ADD_INSULA_TEST(bernstein_polynomial ../textures/interpolators/bernstein_polynomial.cpp ../textures/weights.cpp)
